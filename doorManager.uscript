/*
	Door Manager by Tanese
	======================
	This script allows players to manage doors and gates in the game. Players can whitelist other players to open their doors,
	and the doors automatically close after a specified time.
	Players can also open and close doors by punching them.
	
	========================
	Configuration:
	========================
	- doorCloseTime: Time in seconds after which doors automatically close.

	========================
	Commands:
	========================
	- /whitelistdoor <player>: Whitelist a player to open the door. 
	  Permission: doorManager.whitelist

	- /removewhitelistdoor <player>: Remove a player from the whitelist.
	  Permission: doorManager.whitelist
*/

doorCloseTime = 5; // Time in seconds after which doors automatically close

doorData = map();

event onLoad(){
	serverpackageinstall();
}

event onPlayerGestured(player, gesture) 
{
	if (gesture == "PUNCH_RIGHT")
	{
		barr = player.look.getBarricade();
		if (barr != null)
		{
			if (barr.itemType != "DOOR" and barr.itemType != "GATE") 
			{
				return;
			}
			door = barr;
			if (door.owner == player.id or isPlayerWhitelisted(door.instanceId, player.id) == true)
			{
				doorOpen(door.instanceId);
			}
		}
	}
	if (gesture == "PUNCH_LEFT")
	{
		barr = player.look.getBarricade();
		if (barr != null)
		{
			if (barr.itemType != "DOOR" and barr.itemType != "GATE") 
			{
				if (barr.owner == player.id or isPlayerWhitelisted(barr.instanceId, player.id) == true)
				{
					doorClose(barr.instanceId);
				}
			}
		}
	}
}

function doorOpen(doorId)
{
	door = server.findBarricade(doorId);
	if (door != null)
	{
		door.door.open();
		doorData.set(doorId, time.now.totalSeconds);
	}
}

function doorClose(doorId)
{
	door = server.findBarricade(doorId);
	if (door != null)
	{
		door.door.close();
	}
}

event onInterval(1)
{
	currentTime = time.now.totalSeconds;
	doorsToClose = [];
	foreach (doorId in doorData.keys)
	{
		lastOpenTime = doorData.get(doorId);
		totalTime = currentTime - lastOpenTime;
		if (totalTime >= doorCloseTime)
		{
			doorsToClose.add(doorId);
		}
	}
	foreach (doorId in doorsToClose)
	{
		doorClose(doorId);
		doorData.remove(doorId);
	}
}

function isPlayerWhitelisted(doorId, playerId) 
{
	whitelist = database.firstRow("SELECT * FROM doorWhitelist WHERE doorId=@p0 AND playerId=@p1", doorId, playerId);
	if (whitelist.count != 0)
	{
		return true;
	}
	return false;
}

command whitelistdoor(target)
{
	permission = "doorManager.whitelist";
	execute()
	{
		barr = player.look.getBarricade();
		if (barr != null)
		{
			if (arguments[0] == null)
			{
				player.message("Wrong Usage! Correct Usage: /whitelistdoor <player>");
				return;
			}
			target = toPlayer(arguments[0]);
			if (target == null)
			{
				player.message("No player found!");
				return;
			}
			if (barr.itemType != "DOOR" and barr.itemType != "GATE") 
            {
				player.message("You must be looking at a door or gate to use this command.");
				return;
			}
			door = barr;
			if (door.owner == player.id)
			{
				targetPlayerId = target.id;
				doorId = door.instanceId;
				if (!isPlayerWhitelisted(doorId, targetPlayerId))
				{
					database.nonQuery("INSERT INTO doorWhitelist (doorId, playerId) VALUES (@p0, @p1);", doorId, targetPlayerId);
					player.message("Player " + target.name + " has been whitelisted for this door.");
				}
				else
				{
					player.message("Player " + targetPlayerId + " is already whitelisted for this door.");
				}
			}
			else
			{
				player.message("You do not own this door.");
			}
		}
		else
		{
			player.message("You must be looking at a door or gate to use this command.");
		}
	}
}

command removewhitelistdoor(target)
{
	permission = "doorManager.whitelist";
	execute()
	{
		barr = player.look.getBarricade();
		if (barr != null)
		{
			if (arguments[0] == null)
			{
				player.message("Wrong Usage! Correct Usage: /removewhitelistdoor <player>");
				return;
			}
			target = toPlayer(arguments[0]);
			if (target == null)
			{
				player.message("No player found!");
				return;
			}
			if (barr.itemType != "DOOR" and barr.itemType != "GATE")
			{
				player.message("You must be looking at a door or gate to use this command.");
				return;
			}
			door = barr;
			if (door.owner == player.id)
			{
				targetPlayerId = target.id;
				if (isPlayerWhitelisted(door.instanceId, targetPlayerId))
				{
					database.nonQuery("DELETE FROM doorWhitelist WHERE doorId=@p0 AND playerId=@p1;", door.instanceId, targetPlayerId);
					player.message("Player " + target.name + " has been removed from the whitelist for this door.");
				}
				else
				{
					player.message("Player " + target.name + " is not whitelisted for this door.");
				}
			}
			else
			{
				player.message("You do not own this door.");
			}
		}
		else
		{
			player.message("You must be looking at a door or gate to use this command.");
		}
	}
}

function serverpackageinstall(){
	databaseinstall();
	logger.log("Door Manager by Tanese Successfully Loaded!");
}

function databaseinstall(){
	if (database.allRows("SELECT count(*) as Exist from INFORMATION_SCHEMA.TABLES where table_name = 'doorWhitelist';")[0][0] == "0")
	{
		database.nonQuery("CREATE TABLE IF NOT EXISTS doorWhitelist(doorId VARCHAR(255), playerId VARCHAR(255));");
	}
}
